---
layout: posts
title: "将多个Excel中的内容合并到一个Excel中多个Sheet中"
---    
## Excel内容合并(多Excel内容合并到一个Excel中多个Sheet中)
-------------------------------------------  
###方法一：合并到新建的Excel文件中
* 将需要合并的Excel文件放在同一个文件夹中，并在这个文件夹中新建一个Excel文件       
* 打开新建的Excel文件，按下Alt+F11，打开宏，新建一个模组(如下图)    
![新建模组](/images/excel/a.jpg)
* 拷贝以下代码到打开的窗口中去
<font size=4px>
<xmp class="prettyprint linenums">
Sub CombineWorkbooks()
    Dim FilesToOpen
    Dim x As Integer
    On Error GoTo ErrHandler
    Application.ScreenUpdating = False
    FilesToOpen = Application.GetOpenFilename _
      (FileFilter:="MicroSoft Excel文件(*.xls),*.xls", _
      MultiSelect:=True, Title:="要合并的文件")
    If TypeName(FilesToOpen) = "Boolean" Then
       MsgBox "没有选中文件"
       GoTo ExitHandler
    End If
    x = 1
    While x <= UBound(FilesToOpen)
        Workbooks.Open Filename:=FilesToOpen(x)
        Sheets().Move after:=ThisWorkbook.Sheets _
        (ThisWorkbook.Sheets.Count)
        x = x + 1
    Wend
ExitHandler:
    Application.ScreenUpdating = True
    Exit Sub
ErrHandler:
    MsgBox Err.Description
    Resume ExitHandler
End Sub
</xmp>
</font>
执行命令(运行--运行子过程/用户窗体)，弹出对话框，批量选择要合并的Excel文件即可。注意，完成后，内容合并到了新建的那个Excel文件中了。     
###方法二：不合并到新建的Excel中
* 将需要合并的Excel文件放在同一个文件夹中，并在这个文件夹中新建一个Excel文件       
* 打开新建的Excel文件，按下Alt+F11，打开宏，新建一个模组     
* 拷贝以下代码到打开的窗口中去
<font size=4px>
<xmp class="prettyprint linenums">
Sub Books2Sheets()
'定义对话框变量
Dim fd As FileDialog
Set fd = Application.FileDialog(msoFileDialogFilePicker)
'新建一个工作簿
Dim newwb As Workbook
Set newwb = Workbooks.Add
With fd
If .Show = -1 Then
'定义单个文件变量
Dim vrtSelectedItem As Variant
'定义循环变量
Dim i As Integer
i = 1
'开始文件检索
For Each vrtSelectedItem In .SelectedItems
'打开被合并工作簿
Dim tempwb As Workbook
Set tempwb = Workbooks.Open(vrtSelectedItem)
'复制工作表
tempwb.Worksheets(1).Copy Before:=newwb.Worksheets(i) '把新工作簿的工作表名字改成被复制工作簿文件名，这儿应用于xls文件，即Excel97-2003的文件，如果是Excel2007，需要改成xlsx
newwb.Worksheets(i).Name = VBA.Replace(tempwb.Name, ".xls", "")
'关闭被合并工作簿
tempwb.Close SaveChanges:=False
i = i + 1
Next vrtSelectedItem
End If
End With
Set fd = Nothing
End Sub
</xmp>
</font>
执行命令(运行--运行子过程/用户窗体)，弹出对话框，批量选择要合并的Excel文件即可。此时所有内容将合并到某一个Excel文件中，而不是新建的Excel文件中。